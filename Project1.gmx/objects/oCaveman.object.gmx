<!--This Document is generated by GameMaker, if you edit it by hand then you do so at your own risk!-->
<object>
  <spriteName>sCavemanLeft</spriteName>
  <solid>0</solid>
  <visible>-1</visible>
  <depth>60</depth>
  <persistent>0</persistent>
  <parentName>oEnemy</parentName>
  <maskName>&lt;undefined&gt;</maskName>
  <events>
    <event eventtype="0" enumb="0">
      <action>
        <libid>1</libid>
        <id>604</id>
        <kind>0</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>0</useapplyto>
        <exetype>1</exetype>
        <functionname>action_inherited</functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
      </action>
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>makeActive();
setCollisionBounds(2, 0, sprite_width-2, sprite_height);
xVel = 2.5;
image_speed = 0.5;
myGrav = 0.6;

// stats
type = "Caveman";
hp = 3;
invincible = 0;
favor = 2;

IDLE = 0;
WALK = 1;
ATTACK = 2;
STUNNED = 98;
DEAD = 99;
status = IDLE;

bounced = false;
dead = false;
counter = 0;
sightCounter = 0;

LEFT = 0;
RIGHT = 1;
facing = RIGHT;

shakeCounter = 0;
shakeToggle = 1;</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="3" enumb="0">
      <action>
        <libid>1</libid>
        <id>604</id>
        <kind>0</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>0</useapplyto>
        <exetype>1</exetype>
        <functionname>action_inherited</functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
      </action>
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>if (active)
{
moveTo(xVel,yVel);

if (not held) yVel += myGrav;
if (yVel &gt; yVelLimit) yVel = yVelLimit;

colLeft = false;
colRight = false;
colBot = false;
colTop = false;
if (isCollisionLeft(1)) colLeft = true;
if (isCollisionRight(1)) colRight = true;
if (isCollisionBottom(1)) colBot = true;
if (isCollisionTop(1)) colTop = true;

if (status &gt;= STUNNED)
{
    if (collision_point(x+8, y+12, oSolid, 0, 0))
    {
        scrCreateBlood(x+8, y+8, 3);
        playSound(global.sndCavemanDie);
        instance_destroy();
    }
}
else if (not held)
{
    if (collision_point(x+8, y+8, oSolid, 0, 0))
    {
        scrCreateBlood(x+8, y+8, 3);
        playSound(global.sndCavemanDie);
        instance_destroy();
    }
}

if (status != DEAD and status != STUNNED and hp &lt; 1)
{
    status = DEAD;
}

if (colBot and status != STUNNED)
    yVel = 0;

if (status == IDLE)
{
    bounced = false;
    if (colBot and
        (collision_point(x-1, y, oSolid, -1, -1) or collision_point(x+16, y, oSolid, -1, -1)))
    {
        yVel = -6;
        if (facing == LEFT) xVel = -1;
        else xVel = 1;
        counter -= 10;
    }
    
    if (yVel &lt; 0 and colTop)
    {
        yVel = 0;
    }

    if (colBot and counter &gt; 0) counter -= 1;    
    if (counter &lt; 1)
    {
        facing = rand(0,1);
        status = WALK;
    }
    if (sightCounter &gt; 0) sightCounter -= 1;
    else
    {
        sight = instance_create(x, y, oEnemySight);
        if (facing == LEFT) sight.direction = 180;
        else sight.direction = 0;
        sight.speed = 10;
        sight.owner = instance_place(x, y, oCaveman);
        sightCounter = 5;
    }
}
else if (status == WALK)
{
    if (colLeft or colRight)
    {
        if (facing == LEFT) facing = RIGHT;
        else facing = LEFT;
    }
    
    if (facing == LEFT)
    {
        if (not collision_point(x-1, y+16, oSolid, -1, -1))
        {
            status = IDLE;
            counter = rand(20,50);
            xVel = 0;
        }
        xVel = -1.5;
    }
    else
    {
        if (not collision_point(x+16, y+16, oSolid, -1, -1))
        {
            status = IDLE;
            counter = rand(20,50);
            xVel = 0;
        }
        xVel = 1.5;
    }
    
    if (rand(1,100) == 1)
    {
        status = IDLE;
        counter = rand(20,50);
        xVel = 0;
    }
    
    if (sightCounter &gt; 0) sightCounter -= 1;
    else
    {
        sight = instance_create(x, y, oEnemySight);
        if (facing == LEFT) sight.direction = 180;
        else sight.direction = 0;
        sight.speed = 10;
        sight.owner = instance_place(x, y, oCaveman);
        sightCounter = 5;
    }
}
else if (status == ATTACK)
{
    image_speed = 1;
    if (colLeft or colRight)
    {
        if (facing == LEFT) facing = RIGHT;
        else facing = LEFT;
    }
    if (facing == LEFT) xVel = -3;
    else xVel = 3;
}
else if (status == STUNNED)
{
    if (xVel == 0 and hp &gt; 0) sprite_index = sCavemanStunL;
    else if (bounced)
    {
        if (yVel &lt; 0) sprite_index = sCavemanBounceL;
        else sprite_index = sCavemanFallL;
    }
    else
    {
        if (abs(xVel) &gt; 0) sprite_index = sCavemanDieLL;
        else sprite_index = sCavemanDieLR;
    }
    
    if (colBot and not bounced)
    {
        bounced = true;
        scrCreateBlood(x+8, y+8, 1);
    }
    
    if (held or colBot)
    {      
        if (counter &gt; 0) counter -= 1;
        else if (hp &gt; 0)
        {
            status = IDLE;
            if (held)
            {
                held = false;
                with oPlayer1 { holdItem = 0; pickupItemType = ""; }
            }
        }
    }
}
else if (status == DEAD)
{
    if (not dead)
    {
        if (countsAsKill)
        {
            if (isRealLevel()) global.enemyKills[4] += 1;
            global.cavemen += 1;
            global.kills += 1;
        }
        playSound(global.sndCavemanDie);
        dead = true;
    }

    sprite_index = sCavemanDeadL;
    
    if (abs(xVel) &gt; 0 or abs(yVel) &gt; 0) status = STUNNED;
}

if (status &gt;= STUNNED)
{
    scrCheckCollisions();
    
    if (xVel == 0 and yVel == 0 and hp &lt; 1) status = DEAD;
}

if (xVel &gt; 0) xVel -= 0.1;
if (xVel &lt; 0) xVel += 0.1;
if (abs(xVel) &lt; 0.5) xVel = 0;

if (status &lt; STUNNED)
{
    if (abs(xVel) &gt; 0) sprite_index = sCavemanRunLeft;
    else sprite_index = sCavemanLeft;
}
if (held)
{
    if (hp &gt; 0) sprite_index = sCavemanHeldL;
    else sprite_index = sCavemanDHeldL;
}
}</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="4" ename="oWhipPre">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>if (status &lt; STUNNED or other.type == "Machete")
{
    hp -= other.damage;
    if (bloodLeft &gt; 0)
    {
        scrCreateBlood(x+sprite_width/2, y+sprite_height/2, 1);
        if (hp &lt; 0) bloodLeft -= 1;
    }
    countsAsKill = true;
    status = STUNNED;
    counter = stunTime;
    yVel = -3;
    if (other.x &lt; (x+8)) xVel = 2;
    else xVel = -2;
    image_speed = 0.5;
    playSound(global.sndHit);
}</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="4" ename="oWhip">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>if (status &lt; STUNNED or other.type == "Machete")
{
    hp -= other.damage;
    if (bloodLeft &gt; 0)
    {
        scrCreateBlood(x+sprite_width/2, y+sprite_height/2, 1);
        if (hp &lt; 0) bloodLeft -= 1;
    }
    countsAsKill = true;
    status = STUNNED;
    counter = stunTime;
    yVel = -3;
    if (other.x &lt; (x+8)) xVel = 2;
    else xVel = -2;
    image_speed = 0.5;
    playSound(global.sndHit);
}</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="4" ename="oCharacter">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>// jumped on
if (abs(other.x-(x+8)) &gt; 8)
{
    // do nothing
}
else if (not other.dead and (other.state == 15 or other.state == 16) and other.y &lt; y+5 and not other.swimming)
{
    if (status &lt; STUNNED)
    {
        other.yVel=-6-0.2*other.yVel
        if (global.hasSpikeShoes) { hp -= (3 * (floor(other.fallTimer/16)+1)); if (not bloodless) instance_create(other.x, other.y+8, oBlood); }
        else hp -= (1 * (floor(other.fallTimer/16)+1));
        other.fallTimer = 0;
        countsAsKill = true;
        status = STUNNED;
        counter = stunTime;
        yVel = -6;
        if (other.x &lt; x+8) xVel += 1;
        else xVel -= 1;
        image_speed = 0.5;
        playSound(global.sndHit);
    }
}
else if (other.invincible == 0)
{
    if (status &lt; STUNNED)
    {
        other.blink = 30
        other.invincible = 30
        if (other.y &lt; y)
            other.yVel = -6
        if (other.x &lt; x)
            other.xVel = -6
        else
            other.xVel = 6
        instance_create(other.x, other.y, oBlood);
    
        if (global.plife &gt; 0)
        {
            global.plife -= 1;
            if (global.plife &lt;= 0 and isRealLevel()) global.enemyDeaths[4] += 1;
        }
        playSound(global.sndHurt);
    }
}</string>
          </argument>
        </arguments>
      </action>
    </event>
  </events>
  <PhysicsObject>0</PhysicsObject>
  <PhysicsObjectSensor>0</PhysicsObjectSensor>
  <PhysicsObjectShape>0</PhysicsObjectShape>
  <PhysicsObjectDensity>0</PhysicsObjectDensity>
  <PhysicsObjectRestitution>0</PhysicsObjectRestitution>
  <PhysicsObjectGroup>0</PhysicsObjectGroup>
  <PhysicsObjectLinearDamping>0</PhysicsObjectLinearDamping>
  <PhysicsObjectAngularDamping>0</PhysicsObjectAngularDamping>
  <PhysicsObjectFriction>0.200000002980232</PhysicsObjectFriction>
  <PhysicsObjectAwake>-1</PhysicsObjectAwake>
  <PhysicsObjectKinematic>0</PhysicsObjectKinematic>
  <PhysicsShapePoints/>
</object>
